buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id 'com.google.protobuf' version '0.8.18'
}

apply plugin: 'java'
// Inform IntelliJ projects about the generated code.
apply plugin: 'idea'
// Provide convenience executables for trying out the examples.
apply plugin: 'application'

repositories {
  mavenCentral()
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots/'
  }
}

ext {
  grpcVersion = '1.44.1'
}

dependencies {
  implementation "javax.annotation:javax.annotation-api:1.3.2"
  implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.20.0'
  implementation "com.google.guava:guava:31.1-jre"
  implementation "com.google.api.grpc:proto-google-common-protos:2.8.0"
  implementation "io.grpc:grpc-netty:${grpcVersion}"
  implementation "io.grpc:grpc-protobuf:${grpcVersion}"
  implementation "io.grpc:grpc-stub:${grpcVersion}"

  implementation "javax.servlet:javax.servlet-api:4.0.1"
  implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '11.0.16'
  implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '11.0.8'


  implementation "com.google.api.graphql:rejoiner:0.4.0-SNAPSHOT"
  implementation "com.google.api.graphql:rejoiner-grpc:0.4.0-SNAPSHOT"
  implementation "com.google.api.graphql:rejoiner-guice:0.4.0-SNAPSHOT"
  implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'
  implementation "com.google.inject.extensions:guice-servlet:5.1.0"
  implementation "net.javacrumbs.future-converter:future-converter-java8-guava:1.1.0"
}

sourceSets {
  main {
    java {
      srcDirs 'build/generated/source/proto/main/grpc'
      srcDirs 'build/generated/source/proto/main/java'
    }
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.19.4'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all().each { task -> task.dependsOn updateSubmodules }
    all()*.plugins {
      grpc {}
    }
  }
}

idea {
  module {
    // Not using generatedSourceDirs because of
    // https://discuss.gradle.org/t/support-for-intellij-2016/15294/8
    sourceDirs += file("${projectDir}/build/generated/source/proto/main/java");
    sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc");
  }
}

task temporalGraphqlServer(type: CreateStartScripts) {
  mainClassName = 'io.temporal.graphql.GraphQLServer'
  applicationName = 'temporal-graphql-server'
  outputDir = new File(project.buildDir, 'tmp')
  classpath = jar.outputs.files + project.configurations.runtimeClasspath
}

applicationDistribution.into('bin') {
  from(temporalGraphqlServer)
  fileMode = 0755
}

task initSubmodules(type: Exec) {
  description = 'Initializes submodules'
  commandLine 'git', 'submodule', 'init'
}

task updateSubmodules(type: Exec) {
  dependsOn initSubmodules
  description = 'Update submodules'
  commandLine 'git', 'submodule', 'update'
}
